
task gradleWrapper(type: Wrapper) { gradleVersion = '3.4.1' }

def getGitVersionName() {
	def stdout = new ByteArrayOutputStream()
	project.exec {
		commandLine 'git', 'describe', '--tags', '--always'
		standardOutput = stdout
	}
	def rawVersion = stdout.toString().trim()
	return rawVersion.replaceFirst(/^[vV]([^a-zA-Z].*$)/, '$1')
}

allprojects {
	group 'net.tetrakoopa.poignee'
}

subprojects {

	repositories {
		mavenCentral()
		mavenLocal()
		maven { url 'https://jitpack.io' }
	}

	apply plugin: 'groovy'
	apply plugin: 'maven'

	dependencies {
		testCompile('org.spockframework:spock-core:1.1-groovy-2.4') {
			exclude group: 'org.codehaus.groovy', module: 'groovy-all'
		}
	}

	if (project.plugins.hasPlugin("org.gradle.java")) {

		dependencies {
			compile gradleApi()
			compile localGroovy()
			testCompile 'junit:junit:4.12'
		}

		task sourcesJar(type: Jar, dependsOn:classes) {
			classifier = 'sources'
			from sourceSets.main.allSource
		}

		artifacts {
			archives sourcesJar
		}

		install {
			repositories.mavenInstaller {
				pom.project {
					licenses {
						license {
							name 'The Apache Software License, Version 2.0'
							url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
							distribution 'repo'
						}
					}
				}
			}
		}

		compileGroovy.doFirst {
			sourceCompatibility = "${project.property('sourceCompatibility')}"
			targetCompatibility = "${project.property('targetCompatibility')}"
		}

	}

}

